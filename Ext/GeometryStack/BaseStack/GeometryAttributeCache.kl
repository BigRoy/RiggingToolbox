/*
 *  Copyright 2010-2014 Fabric Engine Inc. All rights reserved.
 */

require Math;
require Geometry;


object GeometryAttributeCache : CachePoint {
  // Temp: Used in debugging and unit testing.
  Boolean disabled;
  Boolean valid;

  String attributesNames[];
  GeometryAttribute cachedAttributes[][];
  UInt32 attributeVersions[][];
};

function GeometryAttributeCache() {
}


function GeometryAttributeCache.disable!() {
  this.disabled = true;
}

function GeometryAttributeCache.enable!() {
  this.disabled = false;
}

function GeometryAttributeCache.invalidate!() {
  this.valid = false;
}

function Boolean GeometryAttributeCache.isValid() {
  return this.valid;
}

function GeometryAttributeCache.setCachedAttributeNames!(String attributesNames[]) {
  this.attributesNames = attributesNames.clone();
}

function GeometryAttributeCache.update!(GeometrySet geomSet, String attributesNames[]) {
  if(this.disabled)
    return;
  AutoProfilingEvent p(FUNC);
  this.attributesNames = attributesNames.clone();
  this.cachedAttributes.resize(geomSet.size);
  this.attributeVersions.resize(geomSet.size);
  for(Integer i=0; i<geomSet.size; i++){
    Ref<GeometryAttributes> attributes = geomSet.get(i).getAttributes();
    this.cachedAttributes[i].resize(this.attributesNames.size);
    this.attributeVersions[i].resize(this.attributesNames.size);
    for(Integer j=0; j<this.cachedAttributes.size; j++){
      Ref<GeometryAttribute> attr = attributes.getAttribute(this.attributesNames[i]);
      if(attr){
        this.cachedAttributes[i][j] = cloneAttribute(attr);
        this.attributeVersions[i][j] = attr.getVersion();
      }
      else{
        // report("Warning: geometry does not have required attribute ");
        report("Warning in GeometryAttributeCache.init: Geometry " +getGeomDebugName(geomSet.get(i))+ " at index :"+i+" does not have the following attribute:" + this.attributesNames[i]);// Maybe the deformer will generate it. 
      }
    }
  }
  this.valid = true;
}

/// Restores the geometry attributes to the state found in the cache.
function GeometryAttributeCache.restore(io GeometrySet geomSet){
  if(this.disabled)
    return;
  AutoProfilingEvent p(FUNC);
  for(Integer i=0; i<geomSet.size; i++){
    Ref<GeometryAttributes> attributes = geomSet.get(i).getAttributes();
    for(Integer j=0; j<this.attributesNames.size; j++){
      Ref<GeometryAttribute> attr = attributes.getAttribute(this.attributesNames[j]);
      if(this.cachedAttributes[i][j] != null){
        if(this.attributeVersions[i][j] != attr.getVersion()){
          Ref<Object> cachedAttr = this.cachedAttributes[i][j];
          attr.copyFrom( cachedAttr );
        }
      }
      else{
        // The attribute did not exist before. 
        // remove the attribute?
      }
    }
  }
}


function GeometryAttributeCache.free!(){
  AutoProfilingEvent p(FUNC);
  this.cachedAttributes.resize(0);
  this.attributeVersions.resize(0);
  this.valid = true;
}