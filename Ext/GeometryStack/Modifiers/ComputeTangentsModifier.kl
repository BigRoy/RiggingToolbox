/*
 *  Copyright 2010-2014 Fabric Engine Inc. All rights reserved.
 */

require Math;
require Geometry;

//////////////////////////////////////
// Factory definition.

object ComputeTangentsModifierFactory : GeometryOperatorFactory {
};

function GeometryOperator ComputeTangentsModifierFactory.constructGeometryOperator(){
  ComputeTangentsModifier geomOp();
  return geomOp;
}


//////////////////////////////////////
//


object ComputeTangentsModifier : BaseModifier {
  
};


function UInt32[String] ComputeTangentsModifier.getAttributeInteractions(){
  UInt32 result[String];
  result['positions'] = AttrMode_Read;
  result['uvs0'] = AttrMode_Read;
  result['tangents'] = AttrMode_Write;
  return result;
}

/// Per-geometry computation of the push. 
/// \internal
operator computeTangentsModifier_modifyGeometries<<<index>>>(
  io GeometrySet geomSet,
  Boolean useGPU
){
  PolygonMesh mesh = geomSet.get(index);
  if(mesh)
    mesh.recomputeTangentsIfRequired();
}

function ComputeTangentsModifier.evaluate!(EvalContext context, io GeometrySet geomSet){
  AutoProfilingEvent p(FUNC);
  computeTangentsModifier_modifyGeometries<<<geomSet.size()>>>(geomSet, this.useGPU());
}


